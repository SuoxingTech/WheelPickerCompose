plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

android {
    compileSdk 33

    defaultConfig {
        minSdk 26
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }
    namespace "com.commandiron.wheel_picker_compose"

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += [
                "-Xopt-in=dev.chrisbanes.snapper.ExperimentalSnapperApi"
        ]
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_compiler_version
    }
}

dependencies {
    implementation(platform("androidx.compose:compose-bom:2024.04.00"))
    implementation("androidx.compose.ui:ui")
    implementation("androidx.compose.material3:material3")

    implementation "dev.chrisbanes.snapper:snapper:0.3.0"
}

afterEvaluate {
    publishing{

        def props = new Properties()
        props.load(new FileInputStream(new File(rootProject.rootDir, "github.properties")))

        def user = props.getProperty("gpr.user")
        def key = props.getProperty("gpr.key")

        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/SuoxingTech/WheelPickerCompose")
                credentials {
                    username = user
                    password = key
                }
            }
        }
        publications {
            release(MavenPublication){
                from components.release

                groupId = 'dev.suoxing.tech'
                artifactId = 'wheel-picker-compose'
                version = '1.2.1'
            }
        }
    }
}